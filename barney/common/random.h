https://www.shadertoy.com/view/md3BDs

// ---8<----------------------------------------------------------------------
// Random number generator

// https://www.pcg-random.org/
uint pcg(uint v)
{
	uint state = v * 747796405u + 2891336453u;
	uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
	return (word >> 22u) ^ word;
}

uvec2 pcg2d(uvec2 v)
{
    v = v * 1664525u + 1013904223u;

    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;

    v = v ^ (v>>16u);

    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;

    v = v ^ (v>>16u);

    return v;
}

// http://www.jcgt.org/published/0009/03/02/
uvec3 pcg3d(uvec3 v) {

    v = v * 1664525u + 1013904223u;

    v.x += v.y*v.z;
    v.y += v.z*v.x;
    v.z += v.x*v.y;

    v ^= v >> 16u;

    v.x += v.y*v.z;
    v.y += v.z*v.x;
    v.z += v.x*v.y;

    return v;
}

// http://www.jcgt.org/published/0009/03/02/
uvec3 pcg3d16(uvec3 v)
{
    v = v * 12829u + 47989u;

    v.x += v.y*v.z;
    v.y += v.z*v.x;
    v.z += v.x*v.y;

    v.x += v.y*v.z;
    v.y += v.z*v.x;
    v.z += v.x*v.y;

	v >>= 16u;

    return v;
}

// http://www.jcgt.org/published/0009/03/02/
uvec4 pcg4d(uvec4 v)
{
    v = v * 1664525u + 1013904223u;
    
    v.x += v.y*v.w;
    v.y += v.z*v.x;
    v.z += v.x*v.y;
    v.w += v.y*v.z;
    
    v ^= v >> 16u;
    
    v.x += v.y*v.w;
    v.y += v.z*v.x;
    v.z += v.x*v.y;
    v.w += v.y*v.z;
    
    return v;
}

uvec3 seed = uvec3(0);
void initSeed(vec2 fragCoord)
{
    // FIXME: how to eliminate time glitches?
    seed = uvec3(fragCoord, iTime * 1e5);
    seed.zy ^= pcg2d(seed.xz);
}

float rand1()
{
    seed = seed.yzx;
    seed.x = pcg(seed.x);
    return float(seed.x) * (1.0 / float(0xffffffffu));
}

vec2 rand2()
{
    seed = seed.yzx;
    seed.xy = pcg2d(seed.xy);
    return vec2(seed.xy) * (1.0 / float(0xffffffffu));
}

vec3 rand3()
{
    seed = pcg3d(seed);
    return vec3(seed) * (1.0 / float(0xffffffffu));
}
